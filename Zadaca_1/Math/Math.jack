class Math
{
    static Array twoToThe;
    
    function void init()
    {
        var int i, pow;
        let twoToThe = Array.new(16);
        
        let pow = 1;
        while (i < 16)
        {
            let twoToThe[i] = pow;
            let pow = pow + pow;
            let i = i + 1;
        } 
        return;
    }
    
    function int multiply(int x, int y)
    {
        var int sum, shifted, i;
        let shifted = x;
        
        while (i < 16)
        {
            if (twoToThe[i] & y)
            {
                let sum = sum + shifted;
            }
            let shifted = shifted + shifted;
            let i = i + 1;
        }
        
        return sum;
    }
    
    function int divide(int x, int y)
    {
        var int negative_x, negative_y, a, result;

        let negative_x = x < 0;
        let negative_y = y < 0;

        let x = Math.abs(x);
        let y = Math.abs(y);

        if (y > x)
        {
            return 0;
        }

        let a = Math.divide(x, y+y);

        if (x-(2*a*y) < y)
        {
            let result = a+a;
        }
        else
        {
            let result = a+a+1;
        }
        
        if (negative_x = negative_y)
        {
            return result;
        }
        else{
            return - result;
        }
    }
    
    function int sqrt(int x)
    {
        var int i, y, pow;
        
        if (x < 0)
        {
            return 0;
        }
        
        let i = 7;
        while (i > -1)
        {
            let pow = Math.multiply(y + twoToThe[i], y + twoToThe[i]);
            if (pow < x)
            {
                let y = y + twoToThe[i];
            }
            let i = i - 1;
        }
        
        return y;
    }

    function int log(int x)
    {
        var int result;
        if (x = 1)
        {
            return 0;
        }

        let result = 0;

        while (x > 1)
        {
            let x = Math.divide(x,2);
            let result = result + 1;
        }

        return result;
    }

    function int pow(int a, int b)
    {
        var int result, base;
        let result = 1;
        let base = a;
        if (b=0)
        {
            return 1;
        }

        while (b>0)
        {
            if(~( Math.multiply(Math.divide(b,2),2 )=b))
            {
                let result = Math.multiply(result, base);
            }

            let base = Math.multiply(base, base);
            let b = Math.divide(b, 2);
        }
        return result;
    }
    
    function int abs(int x)
    {
        if (x < 0)
        {
            return -x;
        }
        return x;
    }
    
    function int min(int x, int y)
    {
        if (x < y)
        {
            return x;
        }
        return y;
    }
    
    function int max(int x, int y)
    {
        if (x > y)
        {
            return x;
        }
        return y;
    }
}
